
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ПустаяСтрока(ПараметрЗапуска) Тогда
		ОбработатьПараметрыЗапуска(ПараметрЗапуска);
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ПроверкаВыполненостиОбновления", ИнтервалОпроса());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверкаВыполненостиОбновления()
	
	Если Не ПрочитатьСобытиеЗавершенияОбновления() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ОбработкаДляЗапуска) Тогда
		ОткрытьОбработку();
	КонецЕсли;
	
	ЗавершитьРаботуСистемы(Ложь);
	
КонецПроцедуры

&НаСервере
Функция ПрочитатьСобытиеЗавершенияОбновления()
	Возврат РеквизитФормыВЗначение("Объект").ПрочитатьСобытиеЗавершенияОбновления();
КонецФункции

&НаСервере
Функция ИнтервалОпроса()
	Возврат РеквизитФормыВЗначение("Объект").ИнтервалОпроса();
КонецФункции

&НаКлиенте
Процедура ОбработатьПараметрыЗапуска(Знач ПараметрЗапуска)
	ОбработкаДляЗапуска = ПрочитатьПараметрыЗапуска(ПараметрЗапуска);
КонецПроцедуры

&НаСервере
Функция ПрочитатьПараметрыЗапуска(Знач Строка)
	Возврат РеквизитФормыВЗначение("Объект").ПрочитатьПараметрыЗапуска(Строка);
КонецФункции

&НаКлиенте
Процедура ОткрытьОбработку()
	
	Адрес = "";
	
	ПоместитьФайл(Адрес, ОбработкаДляЗапуска, , Ложь);
	Имя = ПодключитьВнешнююОбработку(Адрес);
	
	Форма = ПолучитьФорму("ВнешняяОбработка." + Имя + ".Форма.Форма");
	Форма.ОткрытьМодально();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодключитьВнешнююОбработку(Знач Адрес)
	Возврат ВнешниеОбработки.Подключить(Адрес);
КонецФункции

#КонецОбласти
